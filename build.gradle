plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'io.freefair.lombok' version '8.14'
}

group = 'space.yong.orkes'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(24))
    }
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-parameters"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.orkes.conductor:orkes-conductor-client-spring:4.0.11'
    implementation ('com.google.api-client:google-api-client:2.8.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation 'com.google.oauth-client:google-oauth-client:1.39.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.azure:azure-ai-formrecognizer:4.1.13'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'
}

bootRun {
    jvmArgs('--sun-misc-unsafe-memory-access=allow')
}

bootBuildImage {
    publish = System.getenv("DOCKER_PASS") != null
    createdDate = "now"
    environment["BPE_DELIM_JAVA_TOOL_OPTIONS"] = " "
    environment["BPE_APPEND_JAVA_TOOL_OPTIONS"] = "--sun-misc-unsafe-memory-access=allow"
    docker {
        publishRegistry {
            username = System.getenv('DOCKER_USER')
            password = System.getenv('DOCKER_PASS')
        }
    }
}
